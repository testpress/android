import groovy.json.JsonSlurper

apply plugin: 'com.android.application'

ext {
    testpressSDK = '1.3.69'
}

dependencies {

    // Dependency Injection
    implementation 'com.squareup.dagger:dagger:1.0.1'
    implementation 'com.squareup.dagger:dagger-compiler:1.0.1'
    implementation 'com.jakewharton:butterknife:6.1.0'
    implementation "ru.tinkoff.scrollingpagerindicator:scrollingpagerindicator:1.1.0"

    implementation 'com.github.kevinsawicki:wishlist:0.9@aar'

    // Event Bus
    implementation 'com.squareup:otto:1.3.5'

    // Image Loading
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'

    // HTTP
    implementation 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    implementation 'com.squareup.okhttp:okhttp:2.3.0'
    implementation 'com.squareup.retrofit:retrofit:1.9.0'

    // Material Dialog
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    // GreenDao for database
    implementation 'de.greenrobot:greendao:2.0.0'
    
    // Testpress Course(which inbuilt has exam & core module)
    implementation 'com.github.testpress:course:' + testpressSDK
    implementation 'com.github.testpress:store:' + testpressSDK

    // Gcm
    implementation 'com.google.firebase:firebase-messaging:19.0.0'
    implementation 'org.greenrobot:eventbus:3.1.1'

    // Google Sign In
    implementation 'com.google.android.gms:play-services-auth:18.0.0'

    // Facebook SDK
    implementation 'com.facebook.android:facebook-android-sdk:4.20.0'

    // Allow methods more than 64K
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.fragment:fragment:1.2.4"

    // Country code picker
    // https://github.com/hbb20/CountryCodePickerProject/wiki
    implementation "com.hbb20:ccp:2.1.9"

    implementation 'io.sentry:sentry-android:1.7.23'
    implementation 'org.slf4j:slf4j-nop:1.7.25'


    //Unit test
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.4'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.4'

}

def key_alias = "sample"
def key_password = "sample"
def store_file = "testpress_debug_keystore.jks"
def store_password = "sample"

try {
    key_alias = TESTPRESS_KEY_ALIAS
    key_password = TESTPRESS_KEY_PASSWORD
    store_file = TESTPRESS_KEYSTORE_PATH
    store_password = TESTPRESS_KEYSTORE_PASSWORD
} catch (Exception ignored) {
    println 'Failed to find key store details. Social Login is disabled'
}

android {

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28

        def jsonFile = file('src/main/assets/config.json')
        def json = new JsonSlurper().parseText(jsonFile.text)

        versionName json.version
        versionCode json.version_code

        def host_url = json.testpress_site_subdomain + '.testpress.in'
        resValue "string", "host_url", host_url
        buildConfigField "String", "BASE_URL", '"https://' + host_url + '"'
        resValue "string", "app_name", json.app_name
        resValue "string", "version", "v " + json.version
        resValue "string", "share_message", json.share_message
        resValue "string", "facebook_app_id", json.facebook_app_id
        resValue "string", "fb_login_protocol_scheme", json.facebook_app_id
        resValue "string", "server_client_id", json.server_client_id
        resValue "color", "primary", "#" + json.primary_color
        buildConfigField "boolean", "ALLOW_ANONYMOUS_USER", "" + json.allow_anonymous_user
        buildConfigField "boolean", "SCREENSHOT_DISABLED", "" + json.screenshot_disabled
        buildConfigField "boolean", "GROWTH_HACKS_ENABLED", "" + json.is_growth_hacks_enabled
        if (json.display_username_on_video != null) {
            buildConfigField "boolean", "DISPLAY_USERNAME_ON_VIDEO", "" + json.display_username_on_video
        }
        if (json.white_labeled_host_url != null && json.white_labeled_host_url != "") {
            resValue "string", "white_labeled_host_url", json.white_labeled_host_url
        }

        applicationId json.package_name
        resValue "string", "package_name", json.package_name

        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }

    packagingOptions {
        // Exclude file to avoid
        // Error: Duplicate files during packaging of APK
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/proguard/coroutines.pro'
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        config {
            keyAlias key_alias
            keyPassword key_password
            storeFile file(store_file)
            storePassword store_password
        }
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
            minifyEnabled false
            // zipAlign false // this is default for debug
        }
        release {
            signingConfig signingConfigs.config
            // minifyEnabled true
            // proguardFile '..\proguard.cfg'
            // signingConfig signingConfigs.release
            // zipAlign true // this is default for release
            // testPackageName 'in.testpress.testpress.tests'
            // testInstrumentationRunner 'android.test.InstrumentationTestRunner' // this is the default
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

apply plugin: 'com.google.gms.google-services'